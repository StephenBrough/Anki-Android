apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.triplet.play'
def homePath = System.properties['user.home']

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId "com.ichi2.anki"
        minSdkVersion 21
        targetSdkVersion 26
        testApplicationId "com.ichi2.anki.tests"
        vectorDrawables.useSupportLibrary = true

        versionCode 20900105
        versionName "2.9alpha5"
    }

    lintOptions {
        abortOnError false
        disable 'TypographyEllipsis'
    }

    signingConfigs {
        release {
            storeFile file("${homePath}/src/android-keystore")
            keyAlias "nrkeystorealias"
            storePassword System.getenv("KSTOREPWD")
            keyPassword System.getenv("KEYPWD")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    useLibrary 'org.apache.http.legacy'

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !travisBuild
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

play {
    serviceAccountEmail = '294046724212-r3bef6kl46pb9gk0h1pl5rcjmpfrdpjl@developer.gserviceaccount.com'
    pk12File = file("${homePath}/src/583631bdd16d.p12")
    track = 'alpha'
}

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.24.2'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:$rootProject.ext.supportLib"

    // Support
    // Note: the design support library can be quite buggy, so test everything thoroughly before updating it
    implementation "com.android.support:design:$rootProject.ext.supportLib"
    implementation "com.android.support:customtabs:$rootProject.ext.supportLib"
    implementation "com.android.support:recyclerview-v7:$rootProject.ext.supportLib"

    // Sqlite
    implementation 'io.requery:sqlite-android:3.16.0'

    // Material Third Party
    implementation('com.afollestad.material-dialogs:core:0.8.6.2@aar') {
        //exclude group: 'com.android.support'  // uncomment to force our local support lib version
        transitive = true
    }
    implementation 'com.getbase:floatingactionbutton:1.10.1'

    // Logging
    implementation 'com.jakewharton.timber:timber:2.7.1'
    implementation('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true;
    }

    // Gson
    implementation 'com.google.code.gson:gson:2.8.0'
    implementation project(":api")

    // Testing
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.19' // v1 while PowerMock does not fully support v2.
    testImplementation 'org.powermock:powermock-core:1.6.5'
    testImplementation 'org.powermock:powermock-module-junit4:1.6.5'
    testImplementation 'org.powermock:powermock-api-mockito:1.6.5'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.18"


    // Architecture Components
    implementation "android.arch.lifecycle:runtime:$rootProject.ext.architectureComponent"
    implementation "android.arch.lifecycle:extensions:$rootProject.ext.architectureComponent"
    implementation "android.arch.lifecycle:compiler:$rootProject.ext.architectureComponent"
    kapt "android.arch.lifecycle:compiler:$rootProject.ext.architectureComponent"
    annotationProcessor "android.arch.lifecycle:compiler:$rootProject.ext.architectureComponent"
    androidTestAnnotationProcessor "android.arch.lifecycle:compiler:$rootProject.ext.architectureComponent"
    testAnnotationProcessor "android.arch.lifecycle:compiler:$rootProject.ext.architectureComponent"
}

apply from: "testconfig.gradle"
kotlin {
    experimental {
        coroutines "enable"
    }
}
